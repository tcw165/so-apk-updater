apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "io.fabric"
apply from: "$rootDir/android_app_extension.gradle"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "co.sodalabs.apkupdater"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode buildVersionCode(artifact_version)
        versionName artifact_version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_URL", "\"https://sparkpoint-func.azurewebsites.net/api/\""
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled = true
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // Disables PNG crunching for the release build type.
            crunchPngs false
        }
    }

    dexOptions {
        // Don't preDex on CI
        preDexLibraries = !rootProject.build.ci
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
        exclude 'LICENSE.txt'
    }

    aaptOptions {
        cruncherEnabled = false
    }

    lintOptions {
        abortOnError true
        fatal 'StopShip'

        // this is never going to be a real "app"
        disable 'GoogleAppIndexingWarning'
    }

    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
        unitTests {
            all {
                // configure the test JVM arguments
                jvmArgs '-noverify'
                includeAndroidResources = true
                returnDefaultValues = true
            }
        }
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }

    configurations.all {
        resolutionStrategy.force "com.google.code.findbugs:jsr305:3.0.2"
    }
}

kapt {
    useBuildCache = true
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation project(path: ":updaterengine", configuration: "default")

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.2"
    implementation "com.jakewharton.rxrelay2:rxrelay:2.0.0"

    // Support Libs
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation "com.android.support:multidex:1.0.3"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // Better logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // Scaled DP/SP
    implementation 'com.intuit.sdp:sdp-android:1.0.6'
    implementation 'com.intuit.ssp:ssp-android:1.0.6'

    // Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.8@aar') {
        transitive = true
    }

    // Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
