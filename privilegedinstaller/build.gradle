apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
// For full integration of bugs tracking. e.g. mapping upload
apply plugin: 'com.bugsnag.android.gradle'

apply from: rootProject.file("gradleScripts/android_app_extension.gradle")
apply from: rootProject.file("gradleScripts/version_extension.gradle")

// If you are missing these files, please download from our internal file repo
// store: https://drive.google.com/open?id=1IXPvYIFQz0cPlzw88HIJ8M_Ipf7IYSy1
// key:   https://drive.google.com/open?id=1Q6UysSx2CmMPfhFOSwe87nSXvwFZeq5u
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

// Version
def versionPropertiesFile = rootProject.file("version.properties") // The repo (global) properties
def versionProperties = new Properties()
versionProperties.load(new FileInputStream(versionPropertiesFile))

// Bugsnag service
def getBugsnagAPIKeyFromFile = {
    // If you are missing this file, please download from our internal file repo
    // https://drive.google.com/drive/folders/1V8tpGNrdw0P_58fjmFp1oi3S6UhDy5Tv?usp=sharing
    def bugsnagPropertiesFile = project.file("bugsnag.properties")
    def bugsnagProperties = new Properties()
    bugsnagProperties.load(new FileInputStream(bugsnagPropertiesFile))
    bugsnagProperties.bugsnag_api_key
}
def bugsnagAPIKey = System.getenv("BUGSNAG_API_KEY_FOR_PRIVILEGED_INSTALLER") ?: getBugsnagAPIKeyFromFile()

android {
    compileSdkVersion deps.build.compileSdkVersion
    buildToolsVersion deps.build.buildToolsVersion

    defaultConfig {
        applicationId "co.sodalabs.privilegedinstaller"

        minSdkVersion deps.build.minSdkVersion
        targetSdkVersion deps.build.targetSdkVersion

        versionCode buildVersionCode(versionProperties.artifact_version)
        versionName versionProperties.artifact_version

        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BUGSNAG_API_KEY", "\"$bugsnagAPIKey\""

        // Command to extract the public key hash:
        // keytool -printcert -jarfile your.apk | sed -n 's,SHA256:\s*\([A-F0-9:]*\),\1,p' | sed 's,:,,g' | tr A-f a-f
        buildConfigField "String[]", "CLIENT_WHITELIST", "{" +
            "\"c8a2e9bccf597c2fb6dc66bee293fc13f2fc47ec77bc6b2b0d52c11f51192ab8\"" +
            "}"
    }

    signingConfigs {
        platform {
            keyAlias keystoreProperties['releaseKeyAlias']
            keyPassword keystoreProperties['releaseKeyPassword']
            storeFile rootProject.file(keystoreProperties['releaseKeyStore'])
            storePassword keystoreProperties['releaseStorePassword']
        }
    }

    buildTypes {
        debug {
            // Keep IPackageInstallObserver and IPackageDeleteObserver
            useProguard false
            minifyEnabled false
            shrinkResources false

            signingConfig signingConfigs.platform
        }

        preRelease {
            initWith release
            matchingFallbacks = ['release']

            // Note: Disabling debuggable allows the ZIP alignment to be applied,
            // which also allows this build to be baked in the Android image.
            debuggable false
            useProguard false // Was true. FIXME: Can't find IPackageInstallObserver after ProGuard
            minifyEnabled false // Was true. FIXME: Can't find IPackageInstallObserver after ProGuard
            shrinkResources false // Was true. FIXME: Can't find IPackageInstallObserver after ProGuard
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // Disables PNG crunching for the release build type.
            crunchPngs false

            signingConfig signingConfigs.platform
        }

        release {
            // Note: Disabling debuggable allows the ZIP alignment to be applied,
            // which also allows this build to be baked in the Android image.
            debuggable false
            useProguard false // Was true. FIXME: Can't find IPackageInstallObserver after ProGuard
            minifyEnabled false // Was true. FIXME: Can't find IPackageInstallObserver after ProGuard
            shrinkResources false // Was true. FIXME: Can't find IPackageInstallObserver after ProGuard
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // Disables PNG crunching for the release build type.
            crunchPngs false

            signingConfig signingConfigs.platform
        }
    }

    dexOptions {
        // Don't preDex on CI
        preDexLibraries = !rootProject.ext.isCI
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
        exclude 'LICENSE.txt'
    }

    aaptOptions {
        cruncherEnabled = false
    }

    lintOptions {
        abortOnError false
        fatal 'StopShip'

        // this is never going to be a real "app"
        disable 'GoogleAppIndexingWarning'
    }

    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
        unitTests {
            all {
                // configure the test JVM arguments
                jvmArgs '-noverify'
                includeAndroidResources = true
                returnDefaultValues = true
            }
        }
    }

    // This configuration is to fix the packing problem that we use androidx
    // namespace (ViewModel) and some of the other modules use older android
    // namespace.
    // e.g. android-23-no-hides.jar
    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }

    configurations.all {
        resolutionStrategy.force "com.google.code.findbugs:jsr305:3.0.2"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android
    implementation deps.androidx.appcompat
    implementation deps.androidx.annotations
    implementation deps.androidx.constraintLayout
    implementation deps.androidx.coreKtx
    // Multi-dex.
    implementation deps.androidx.multidex

    // Kotlin STD Library.
    implementation deps.kotlin.stdlibJdk7

    // RxAndroid
    implementation deps.rx.java
    implementation deps.rx.android
    implementation deps.rx.kotlin
    implementation deps.rx.relay
    implementation deps.rx.binding.lib
    implementation deps.rx.binding.appcompat

    // Tracking
    implementation deps.tracking.timber
    implementation 'com.bugsnag:bugsnag-android:4.17.2'

    // Unit tests.
    testImplementation deps.test.junit
    testImplementation deps.test.mockito

    // Instrumentation tests.
    androidTestImplementation deps.test.runner
    androidTestImplementation deps.test.espresso.core
}

repositories {
    mavenCentral()
}

bugsnag {
    // For the Bugsnag Gradle plugin to upload the mapping file to this key.
    apiKey bugsnagAPIKey
}