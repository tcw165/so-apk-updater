apply from: rootProject.file("gradleScripts/version_extension.gradle")

// Version
def versionPropertiesFile = project.file("version.properties")
def versionProperties = new Properties()
versionProperties.load(new FileInputStream(versionPropertiesFile))

android.applicationVariants.all { variant ->
    def isDebuggable = variant.getBuildType().isDebuggable()
    def appName = versionProperties.artifact_name

    variant.outputs.all { output ->
        if (isDebuggable) {
            outputFileName = "${appName}-${output.baseName}-${variant.versionName}-${getGitHash()}.apk"
        } else {
            outputFileName = "${appName}-${output.baseName}-${variant.versionName}.apk"
        }
    }
}

android.applicationVariants.all { variant ->
    variant.assembleProvider.configure {
        it.doLast {
            if (variant.mappingFile != null && variant.mappingFile.exists()) {
                def isDebuggable = variant.getBuildType().isDebuggable()
                def mappingFileName = "$archivesBaseName-$variant.baseName-mapping.txt"
                (new File(variant.mappingFile.parent, mappingFileName)).delete()

                def appName = versionProperties.artifact_name

                if (isDebuggable) {
                    variant.mappingFile.renameTo(variant.mappingFile.parent +
                        "/$appName-$variant.baseName-${variant.versionName}-${getGitHash()}-mapping.txt")
                } else {
                    variant.mappingFile.renameTo(variant.mappingFile.parent +
                        "/$appName-$variant.baseName-${variant.versionName}-mapping.txt")
                }
            }
        }
    }
}